plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.3.1"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

group = 'com.github.squi2rel'
version = '1.0'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    maven {
        name = "henkelmax.public"
        url = 'https://maven.maxhenkel.de/repository/public'
    }
}

dependencies {
    implementation "org.spigotmc:spigot-api:1.21.4-R0.1-SNAPSHOT"

    compileOnly "io.netty:netty-buffer:4.1.115.Final"
    compileOnly files("lib/minecraft-merged-a172fc6613-1.21.4-loom.mappings.1_21_4.layered+hash.2198-v2.jar")

    implementation files("lib/nymmd.jar")
    compileOnly "de.maxhenkel.voicechat:voicechat-api:2.5.27"
    compileOnly "net.dmulloy2:ProtocolLib:5.4.0"
    compileOnly "it.unimi.dsi:fastutil:8.5.15"
    compileOnly files("lib/Vivecraft_Paper_Extensions.jar")
}

tasks {
    runServer {
        // Configure the Minecraft version for our task.
        // This is the only required configuration besides applying the plugin.
        // Your plugin's jar (or shadowJar if present) will be used automatically.
        minecraftVersion("1.21.4")
    }

    shadowJar {
        archiveClassifier.set('')
    }

    build {
        dependsOn shadowJar
    }
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
